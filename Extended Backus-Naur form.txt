program          ::= 'Graph', ws, identifier, nls,
                     {('Import' | 'Export'), ws, type, nls},
                     activity,
                     {activity | connection-start} ;
                 
activity         ::= activity-type, ws, identifier, ws,
                     path-identifier, op-parameters, nls,
                     {('Import', ws, types | 'Export', ws, type), nls} ;
                 
activity-type    ::= 'Activity'
                   | 'ActivityType'
                   | 'ActivityFlatMap' ;

op-parameters    ::= {':', str-parameter} ;

connection-start ::= connection-main, {':', connection-main}, nls ;

connection-main  ::= connections, '->', connections, {'->', connections} ;

connection       ::= connections, {'->', connections} ;

connections      ::= identifier
                   | '(', connection, {',', connection}, ')' ;

types            ::= type, {',', type} ;
                
type             ::= path-identifier, type-args ;

type-args        ::= ['[', types, ']'] ;
               
identifiers      ::= identifier, {',', identifier} ;
               
identifier       ::= letter, {letter, digit, '_'} ;

path-identifier  ::= identifier, {'.', identifier} ;

str-parameter    ::= ? any sequence of characters that doesn't contain ':' or '\n' ? ;

ws               ::= ? at least one white space or tab character ? ;
                
nl               ::= ? one new line ? ;

nls              ::= [ws], nl, {nl, ws} ;